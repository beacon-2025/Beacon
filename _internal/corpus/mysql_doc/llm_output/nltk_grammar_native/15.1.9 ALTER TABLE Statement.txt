```
S -> ALTER_TABLE_STATEMENT
ALTER_TABLE_STATEMENT -> 'ALTER' 'TABLE' 'table_name_1' ALTER_OPTIONS PARTITION_OPTIONS
ALTER_OPTIONS -> ALTER_OPTION | ALTER_OPTION ',' ALTER_OPTIONS
ALTER_OPTION -> TABLE_OPTIONS | ADD_COLUMN_OPTION | ADD_INDEX_OPTION | ADD_CONSTRAINT_OPTION
	       | DROP_OPTION | ALTER_COLUMN_OPTION | CHANGE_COLUMN_OPTION
	       | CHARACTER_SET_OPTION | KEY_OPTION | ALGORITHM_OPTION | LOCK_OPTION
	       | RENAME_OPTION | WITHOUT_WITH_VALIDATION_OPTION
PARTITION_OPTIONS -> PARTITION_OPTION PARTITION_OPTIONS | ''
PARTITION_OPTION -> ADD_PARTITION_OPTION | DROP_PARTITION_OPTION | DISCARD_IMPORT_PARTITION_OPTION
                  | TRUNCATE_PARTITION_OPTION | COALESCE_PARTITION_OPTION | REORGANIZE_PARTITION_OPTION
		  | EXCHANGE_PARTITION_OPTION | CHECK_PARTITION_OPTION | REMOVE_PARTITIONING_OPTION

TABLE_OPTIONS -> TABLE_OPTION | TABLE_OPTION ',' TABLE_OPTIONS
TABLE_OPTION -> 'AUTOEXTEND_SIZE' '=' 'value_value'
	       | 'AUTO_INCREMENT' '=' 'value_value'
	       | 'AVG_ROW_LENGTH' '=' 'value_value'
	       | 'CHARACTER SET' '=' 'charset_value'
	       | 'CHECKSUM' '=' CHECKSUM_VALUE
	       | 'COLLATE' '=' 'collation_value'
	       | 'COMMENT' '=' 'string_value'
	       | 'COMPRESSION' '=' COMPRESSION_VALUE
	       | 'CONNECTION' '=' 'connect_string_value'
	       | 'DATA DIRECTORY' '=' 'directory_path_value'
	       | 'DELAY_KEY_WRITE' '=' DELAY_KEY_WRITE_VALUE
	       | 'ENCRYPTION' '=' ENCRYPTION_VALUE
	       | 'ENGINE' '=' 'engine_name_option'
	       | 'ENGINE_ATTRIBUTE' '=' 'string_value'
	       | 'INSERT_METHOD' '=' INSERT_METHOD_VALUE
	       | 'KEY_BLOCK_SIZE' '=' 'value_value'
	       | 'MAX_ROWS' '=' 'value_value'
	       | 'MIN_ROWS' '=' 'value_value'
	       | 'PACK_KEYS' '=' PACK_KEYS_VALUE
	       | 'PASSWORD' '=' 'string_value'
	       | 'ROW_FORMAT' '=' ROW_FORMAT_VALUE
	       | 'SECONDARY_ENGINE_ATTRIBUTE' '=' 'string_value'
	       | 'STATS_AUTO_RECALC' '=' STATS_AUTO_RECALC_VALUE
	       | 'STATS_PERSISTENT' '=' STATS_PERSISTENT_VALUE
	       | 'STATS_SAMPLE_PAGES' '=' 'value_value'
	       | 'TABLESPACE' 'tablespace_name_1' STORAGE_OPTION
	       | 'UNION' '=' '(' TABLE_NAME_LIST ')'
CHECKSUM_VALUE -> "'0'" | "'1'"
COMPRESSION_VALUE -> "'ZLIB'" | "'LZ4'" | "'NONE'"
DELAY_KEY_WRITE_VALUE -> "'0'" | "'1'"
ENCRYPTION_VALUE -> "'Y'" | "'N'"
INSERT_METHOD_VALUE -> "'NO'" | "'FIRST'" | "'LAST'"
PACK_KEYS_VALUE -> "'0'" | "'1'" | "'DEFAULT'"
ROW_FORMAT_VALUE -> "'DEFAULT'" | "'DYNAMIC'" | "'FIXED'" | "'COMPRESSED'" | "'REDUNDANT'" | "'COMPACT'"
STATS_AUTO_RECALC_VALUE -> "'DEFAULT'" | "'0'" | "'1'"
STATS_PERSISTENT_VALUE -> "'DEFAULT'" | "'0'" | "'1'"
TABLE_NAME_LIST -> 'new_table_name_1' | 'new_table_name_1' ',' TABLE_NAME_LIST
STORAGE_OPTION -> 'STORAGE' DISK_MEMORY_OPTION | ''
DISK_MEMORY_OPTION -> "'DISK'" | "'MEMORY'"

ADD_COLUMN_OPTION -> 'ADD' 'COLUMN' 'new_col_name_1' 'column_definition_unknown' FIRST_AFTER_OPTION 
ADD_INDEX_OPTION -> 'ADD' INDEX_KEY_OPTION 'new_index_name_1' INDEX_TYPE 'index_type_unknown' '(' KEYPART_LIST ')' INDEX_OPTION_LIST
ADD_CONSTRAINT_OPTION -> 'ADD' 'CONSTRAINT' SYMBOL_OPTION 'PRIMARY KEY' INDEX_TYPE '(' KEYPART_LIST ')' INDEX_OPTION_LIST
		       | 'ADD' 'CONSTRAINT' SYMBOL_OPTION 'UNIQUE' INDEX_KEY_OPTION 'new_index_name_1' INDEX_TYPE '(' KEYPART_LIST ')' INDEX_OPTION_LIST
		       | 'ADD' 'CONSTRAINT' SYMBOL_OPTION 'FOREIGN KEY' 'new_foreign_key_name_1' '(' 'col_name_1' ',' 'col_name_2' ')' 'reference_definition_unknown'
		       | 'ADD' 'CONSTRAINT' SYMBOL_OPTION 'CHECK' '(' 'expr_unknown' ')' ENFORCED_OPTION
SYMBOL_OPTION -> 'symbol_name_1' | ''
INDEX_KEY_OPTION -> 'INDEX' | 'KEY'
INDEX_TYPE -> 'USING' 'BTREE' | 'USING' 'HASH' | ''
KEYPART_LIST -> KEYPART | KEYPART ',' KEYPART_LIST
KEYPART -> 'col_name_1' LENGTH_OPTION ASC_DESC_OPTION | '(' 'expr_unknown' ')' ASC_DESC_OPTION
LENGTH_OPTION -> '(' 'length_value' ')' | ''
ASC_DESC_OPTION -> 'ASC' | 'DESC' | ''
INDEX_OPTION_LIST -> INDEX_OPTION | INDEX_OPTION ',' INDEX_OPTION_LIST | ''
INDEX_OPTION -> 'KEY_BLOCK_SIZE' '=' 'value_value' | 'WITH PARSER' 'parser_name_1' | 'COMMENT' "'string'" | 'VISIBLE'_SWITCH
VISIBLE_SWITCH -> 'VISIBLE' | 'INVISIBLE' | ''
FIRST_AFTER_OPTION -> 'FIRST' | 'AFTER' 'col_name_1' | ''

DROP_OPTION -> 'DROP' CHECK_CONSTRAINT_SWITCH 'symbol_name_1'
	     | 'DROP' COLUMN_SYMBOL_SWITCH 'col_name_1' | 'DROP' INDEX_KEY_OPTION 'index_name_1'
CHECK_CONSTRAINT_SWITCH -> 'CHECK' | 'CONSTRAINT'
COLUMN_SYMBOL_SWITCH -> 'CHECK' | 'CONSTRAINT' | 'COLUMN' | ''
ALTER_COLUMN_OPTION -> 'ALTER' COLUMN_SYMBOL_SWITCH 'col_name_1' COLUMN_DEFINITION_OPTION
ALTER_INDEX_OPTION -> 'ALTER' 'INDEX' 'index_name_1' VISIBLE_SWITCH
CHANGE_COLUMN_OPTION -> 'CHANGE' COLUMN_SYMBOL_SWITCH 'old_col_name_1' 'new_col_name_1' 'column_definition_unknown' FIRST_AFTER_OPTION
COLUMN_DEFINITION_OPTION -> 'SET DEFAULT' DEFAULT_VALUE
			  | 'SET' VISIBLE_SWITCH
			  | 'DROP DEFAULT'
DEFAULT_VALUE -> 'literal_unknown' | '(' 'expr_unknown' ')'
CHARACTER_SET_OPTION -> 'CHARACTER SET' '=' 'charset_value' COLLATE_OPTION
		      | 'CONVERT TO CHARACTER SET' 'charset_value' COLLATE_OPTION
COLLATE_OPTION -> 'COLLATE' '=' 'collation_value' | ''
KEY_OPTION -> ENABLE_DISABLE_KEY
ALGORITHM_OPTION -> 'ALGORITHM' '=' ALGORITHM_OPTION_VALUE
KEY_OPTION -> 'ALGORITHM' '=' ALGORITHM_OPTION_VALUE
ENABLE_DISABLE_KEY -> 'ENABLE' 'KEYS' | 'DISABLE' 'KEYS'
ALGORITHM_OPTION_VALUE -> 'DEFAULT' | 'INSTANT' | 'INPLACE' | 'COPY'
LOCK_OPTION -> 'LOCK' '=' LOCK_OPTION_VALUE
LOCK_OPTION_VALUE -> 'DEFAULT' | 'NONE' | 'SHARED' | 'EXCLUSIVE'
RENAME_OPTION -> 'RENAME' COLUMN_INDEX_SWITCH 'old_name_1' RENAME_TO_AS_SWITCH 'new_name_1'
		| 'RENAME' 'TO' 'new_tbl_name_1'
COLUMN_INDEX_SWITCH -> 'COLUMN' | 'INDEX' | 'KEY' | ''
WITHOUT_WITH_VALIDATION_OPTION -> 'WITHOUT VALIDATION' | 'WITH VALIDATION'
RENAME_TO_AS_SWITCH -> 'TO' | 'AS' | ''

ADD_PARTITION_OPTION -> 'ADD PARTITION' '(' 'partition_definition_unknown' ')'
DROP_PARTITION_OPTION -> 'DROP PARTITION' 'partition_names_unknown'
DISCARD_IMPORT_PARTITION_OPTION -> WORD_PARTITION 'partition_names_unknown_or_all' 'TABLESPACE'
TRUNCATE_PARTITION_OPTION -> 'TRUNCATE PARTITION' 'partition_names_unknown_or_all'
COALESCE_PARTITION_OPTION -> 'COALESCE PARTITION' 'number_value'
REORGANIZE_PARTITION_OPTION -> 'REORGANIZE PARTITION' 'partition_names_unknown' 'INTO' '(' 'partition_definition_unknown' ')'
EXCHANGE_PARTITION_OPTION -> 'EXCHANGE PARTITION' 'partition_name_unknown' 'WITH TABLE' 'table_name_1' PARTITION_VALIDATION
CHECK_PARTITION_OPTION -> MAINTENANCE_OPTION 'partition_names_unknown_or_all'
REMOVE_PARTITIONING_OPTION -> 'REMOVE PARTITIONING'
WORD_PARTITION -> 'DISCARD PARTITION' | 'IMPORT PARTITION'

PARTITION_VALIDATION -> 'WITH VALIDATION' | 'WITHOUT VALIDATION' | ''
MAINTENANCE_OPTION -> 'ANALYZE PARTITION' | 'CHECK PARTITION' | 'OPTIMIZE PARTITION' | 'REBUILD PARTITION' | 'REPAIR PARTITION'
```